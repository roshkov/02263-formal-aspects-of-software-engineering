NET
scheme TIMETABLE = extend NET with
class
  type
    TimeTable = TramId -m-> TimeSlot-list, -- time tables
    TramId = Text, -- tram names
    TimeSlot 	  :: stopId     : StopId
    	     	     arrival    : Time
		     departure  : Time,
    ConnId 	  :: from       : StopId
    	     	     to         : StopId, -- connection names
    ScheduleSlots :: arrivals   : Time-list
    	       	     departures : Time-list,
    ScheduleConn = ConnId -m-> ScheduleSlots,
    ScheduleStop = StopId -m-> ScheduleSlots

    value /* generators */
      -- the an empty timetable
      empty : TimeTable = [],

      -- add to a time table an empty plan for a new tram
      addTram : TramId >< TimeTable -> TimeTable
      addTram(tId, tt) is tt !! [tId +> <..>],

      -- add a stop with arrival time and departure time to the plan for a given tram
      addStop : TramId >< StopId >< Time >< Time >< TimeTable -> TimeTable
      addStop(tId, sId, a, d, tt) is tt !! [tId +> tt(tId)^<.mk_TimeSlot(sId, a, d).>],

      createScheduleConn : TimeTable -> ScheduleConn
      createScheduleConn(tt) is createScheduleConnRec(dom(tt), [], tt),

      createScheduleConnRec : TramId-set >< ScheduleConn >< TimeTable -> ScheduleConn
      createScheduleConnRec(trams, sc, tt) is if card(trams) = 1 then
                                             createScheduleConnForTimeSlots(tt(hd(trams)), sc)
                                          else
                                             let
                                                 tram = hd(trams),
                                                 trams' = trams \ {tram}
                                             in
                                                 createScheduleConnRec(trams', createScheduleConnForTimeSlots(tt(tram), sc), tt)
                                             end
                                          end,

      createScheduleConnForTimeSlots : TimeSlot-list >< ScheduleConn -> ScheduleConn
      createScheduleConnForTimeSlots(tsl, sc) is if len(tsl) < 2 then -- Needs two trains to create an ScheduleSlot
      				       	      	  sc
					      elsif len(tsl) = 2 then
					      	  insertScheduleSlot(hd(tsl), hd(tl(tsl)) ,sc)
					      else
						  let
						      tsFrom = hd(tsl),
						      tsTo = hd(tl(tsl)),
						      tsl' = tl(tsl) -- Only remove first TimeSlot from the list
						  in
						      createScheduleConnForTimeSlots(tsl', insertScheduleSlot(tsFrom, tsTo, sc))
						  end
					      end,

      insertScheduleSlot : TimeSlot >< TimeSlot >< ScheduleConn -> ScheduleConn
      insertScheduleSlot(tsFrom, tsTo, sc) is let
      				       	          connId = mk_ConnId(stopId(tsFrom), stopId(tsTo)),
						  arr = departure(tsFrom), -- Arrival on connection
						  dep = arrival(tsTo) -- Departure of connection 
				    	      in
						  if isInSchedule(connId, sc) then
						      sc !! [connId +> mk_ScheduleSlots(arrivals(sc(connId)) ^ <.arr.>,
						     		          	        departures(sc(connId)) ^ <.dep.>)]
						  else
						      sc !! [connId +> mk_ScheduleSlots(<.arr.>, <.dep.>)]
						  end
				              end,

      createScheduleStop : TimeTable -> ScheduleStop
      createScheduleStop(tt) is createScheduleStopRec(dom(tt), [], tt),

      createScheduleStopRec : TramId-set >< ScheduleStop >< TimeTable -> ScheduleStop
      createScheduleStopRec(trams, ss, tt) is if card(trams) = 1 then
				    	     createScheduleStopForTimeSlots(tt(hd(trams)), ss)
					 else
					     let
					         tram = hd(trams),
					   	 trams' = trams \ {tram}
					     in
						 createScheduleStopRec(trams', createScheduleStopForTimeSlots(tt(tram), ss), tt)
					     end
					 end,

      createScheduleStopForTimeSlots : TimeSlot-list >< ScheduleStop -> ScheduleStop
      createScheduleStopForTimeSlots(tsl, ss) is if len(tsl) = 0 then
      				       	      	  ss
					      elsif len(tsl) = 1 then
					      	  insertScheduleSlot(hd(tsl), ss)
					      else
						  let
						      ts = hd(tsl),
						      tsl' = tl(tsl)
						  in
						      createScheduleStopForTimeSlots(tsl', insertScheduleSlot(ts, ss))
						  end
					      end

  value /* observers */
     -- check whether a tram with a given name exists in a given time table
     isIn : TramId >< TimeTable -> Bool
     isIn(tId, tt) is tId isin dom(tt),

     isInSchedule : StopId >< ScheduleStop -> Bool
     isInSchedule(stopId, ss) is stopId isin dom(ss),

     isInSchedule : ConnId >< ScheduleConn -> Bool
     isInSchedule(connId, sc) is connId isin dom(sc),

     findMaxOverlapRec : Time-list >< Time-list >< Int >< Int -> Int
     findMaxOverlapRec(sArr, sDep, curO, maxO) is if len(sArr) > 0 /\ len(sDep) > 0 then
     			  	   	    	         if hd(sArr) <= hd(sDep) then
							     if curO + 1 > maxO then
							         findMaxOverlapRec(tl(sArr), sDep, curO+1, curO+1)
							     else
							         findMaxOverlapRec(tl(sArr), sDep, curO+1, maxO)
							     end
							 else
							     findMaxOverlapRec(sArr, tl(sDep), curO-1, maxO)
							 end
						     else
						         maxO
						     end
     pre isSorted(sArr) /\ isSorted(sDep),

     findMaxOverlap : Time-list >< Time-list -> Int
     findMaxOverlap(arrs, deps) is findMaxOverlapRec(sort(arrs), sort(deps), 0, 0),

     insertScheduleSlot : TimeSlot >< ScheduleStop -> ScheduleStop
     insertScheduleSlot(ts, ss) is if isInSchedule(stopId(ts), ss) then
				        let
					    stopId = stopId(ts),
					    scheduleSlots = ss(stopId)
					in
					    ss !! [stopId +> mk_ScheduleSlots(arrivals(scheduleSlots) ^
					       	  	     		      <.arrival(ts).>,
									      departures(scheduleSlots) ^
									      <.departure(ts).>)]
					end
				    else
					ss !! [stopId(ts) +> mk_ScheduleSlots(<.arrival(ts).>, <.departure(ts).>)]
				    end,
	  						

     getLargerItems : Time-list >< Time -> Time-list
     getLargerItems(tList, t) is if len(tList) = 0 then
      			       	      <..>
				  elsif len(tList) = 1 /\ hd(tList) > t then
      			       	      <.hd(tList).>
				  elsif hd(tList) > t then
				      <.hd(tList).>^getLargerItems(tl(tList),t)
				  else
				      getLargerItems(tl(tList),t)
				  end,

     getSmallerItems : Time-list >< Time -> Time-list
     getSmallerItems(tList, t) is if len(tList) = 0 then
      			       	      <..>
				   elsif len(tList) = 1 /\ hd(tList) <= t then
      			       	      <.hd(tList).>
				   elsif hd(tList) <= t then
				       <.hd(tList).>^getSmallerItems(tl(tList),t)
				   else
				       getSmallerItems(tl(tList),t)
				   end,
     isSorted : Time-list -> Bool
     isSorted(tList) is (all i : Nat :- i isin inds(tList) =>
     			     (all i' : Nat :- i' isin inds(tList) /\ i < i' =>
			         tList(i) <= tList(i)
			     )
     			 ),

     sort : Time-list -> Time-list
     sort(tList) is if len(tList) <= 1 then
      		       	      tList
			  else
			      sort(getSmallerItems(tl(tList), hd(tList))) ^
			      <.hd(tList).> ^
			      sort(getLargerItems(tl(tList), hd(tList)))
			  end as sortedList
     post isSorted(sortedList)

     

  value /* predicates to check time tables */
     noStopCapacityExceeded : ScheduleStop >< Net -> Bool
     noStopCapacityExceeded(stopsTable, n) is (all sId : StopId :- sId isin dom(stopsTable) =>
     					      	  let
						      arrs = arrivals(stopsTable(sId)),
						      deps = departures(stopsTable(sId))
						  in
						      findMaxOverlap(arrs, deps) <= capacity(sId, n)
						  end
     					      ),

     noConnectionCapacityExceeded : ScheduleConn >< Net -> Bool
     noConnectionCapacityExceeded(scheduleTable, n) is (all c : ConnId :- c isin dom(scheduleTable) =>
     						       	    let
								arrs = arrivals(scheduleTable(c)),
								deps = departures(scheduleTable(c))
							    in
								findMaxOverlap(arrs, deps) <= capacity(from(c), to(c), n)
							    end
     						       ),

     noHeadwayViolation : ConnId >< Time-list >< Net -> Bool
     noHeadwayViolation(connId, tList, n) is (all i : Nat :- i isin inds(tList) =>
     					        (all i' : Nat :- i' isin inds(tList) /\ i < i' =>
					    	    tList(i') - tList(i) >= minHeadway(from(connId), to(connId),n)
					    	)
     					    )
     pre isSorted(tList),

     noHeadwayViolation : ScheduleConn >< Net -> Bool
     noHeadwayViolation(scheduleTable, n) is (all c : ConnId :- c isin dom(scheduleTable) =>
     				       	         noHeadwayViolation(c, sort(departures(scheduleTable(c))),n) /\
						 noHeadwayViolation(c, sort(arrivals(scheduleTable(c))),n)
     				       	     ),

     noDrivingTimeExceeded : ScheduleConn >< Net -> Bool
     noDrivingTimeExceeded(scheduleTable, n) is (all c : ConnId :- c isin dom(scheduleTable) =>
						     let
							arrs = arrivals(scheduleTable(c)),
							deps = departures(scheduleTable(c))
						     in
							 (all i : Nat :- i isin inds(arrs) /\ len(arrs) = len(deps) =>
						     	     arrs(i) - deps(i) <= minDrivingTime(from(c), to(c), n)
						     	 )
						     end
     					     	),

     isWellformed : TimeTable >< Net -> Bool
     isWellformed(timeTable, n) is let
				       scheduleConn = createScheduleConn(timeTable),
				       scheduleStop = createScheduleStop(timeTable)
				   in
				       noStopCapacityExceeded(scheduleStop, n) /\
     		     	  	       noConnectionCapacityExceeded(scheduleConn, n) /\
			   	       noHeadwayViolation(scheduleConn, n) /\
			   	       noDrivingTimeExceeded(scheduleConn, n)
			   	   end

end
